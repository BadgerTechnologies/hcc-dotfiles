#!/bin/bash

# Setup hcochran's prompt.
# We expect that ~/.zsh/gencolors has already been sourced by this shell, so
# that all the color strings therein are already defined.

setopt PROMPT_SUBST


function hccprompt {
    # This is magic for drawing extended character art. I got this stuff
    # from: http://www.aperiodic.net/phil/prompt/
    typeset -A altchar
    set -A altchar ${(s..)terminfo[acsc]}
    PR_SET_CHARSET="%{$terminfo[enacs]%}"
    PR_SHIFT_IN="%{$terminfo[smacs]%}"
    PR_SHIFT_OUT="%{$terminfo[rmacs]%}"
    PR_HBAR=${altchar[q]:--}
    PR_ULCORNER=${altchar[l]:--}
    PR_LLCORNER=${altchar[m]:--}
    PR_LRCORNER=${altchar[j]:--}
    PR_URCORNER=${altchar[k]:--}
    PR_BELL=$(echo -n "\a")

    local maincolor="%{$hyellow%}"
    local hostcolor="%{$hgreen%}"

    local dockercolor="%{$hblue%}"
    local docker_label
    if [[ -n $DOCKER_NAME ]] ; then
        docker_label="${dockercolor}$DOCKER_NAME "
    fi

    local host=$(hostname)
    # Strip domain portion of hostname, if present:
    host=${host%.*}

    PS1="${maincolor}===${maincolor}\
[%T ${docker_label}${hostcolor}${host}${maincolor}]%0(?,, %{$bgred%}%{$hwhite%} Exit %? %{$norm%}${maincolor})"\
'$(_git_ps1)'" %~/
${maincolor}==>%{$norm%} "

}
hccprompt

# Emit a terminal bell if last command exited with non-zero status. This makes
# the tmux window label change to "alert" color. Used to do this in the prompt
# but it would re-asert alert status every time the prompt was redrawn, causing
# lots of false alerts. Doing it in precmd means it will happen only once.
function precmd {
    [[ $? != 0 ]] && echo -n ${PR_BELL}
}

alias sp='source ~/.zsh/setprompt'
