#!/bin/bash

# Setup hcochran's prompt.
# We expect that ~/.zsh/gencolors has already been sourced by this shell, so
# that all the color strings therein are already defined.

setopt PROMPT_SUBST


function hccprompt {
    # This is magic for drawing extended character art. I got this stuff
    # from: http://www.aperiodic.net/phil/prompt/
    typeset -A altchar
    set -A altchar ${(s..)terminfo[acsc]}
    PR_SET_CHARSET="%{$terminfo[enacs]%}"
    PR_SHIFT_IN="%{$terminfo[smacs]%}"
    PR_SHIFT_OUT="%{$terminfo[rmacs]%}"
    PR_HBAR=${altchar[q]:--}
    PR_ULCORNER=${altchar[l]:--}
    PR_LLCORNER=${altchar[m]:--}
    PR_LRCORNER=${altchar[j]:--}
    PR_URCORNER=${altchar[k]:--}
    PR_BELL=$(echo -n "\a")

    # We choose yellow because it stands out. Makes it easy to find
    # command boundary in terminal scroll-back buffer
    local maincolor="%{$hyellow%}"

    # Opening and Time of Day
    PS1="${maincolor}===[%T"

    # If in Docker container, print the container name
    local dockercolor="%{$hblue%}"
    if [[ -n $DOCKER_NAME ]] ; then
        PS1+=" ${dockercolor}$DOCKER_NAME"
    fi

    # ptag variable. User can set this to "tag" this shell
    local ptagcolor="$hcyan"
    if [[ -n $ptag ]]; then
        PS1+=" ${ptagcolor}${ptag}"
    fi

    # Hostname, up to first dot
    PS1+=" ${hgreen}%m${maincolor}"

    # Close the bracketed section
    PS1+="${maincolor}]"

    # Exit status if command exited with Error.
    PS1+="%0(?,, $bgred$hwhite Exit %? $norm${maincolor})"

    # Git info, if applicable
    PS1+='$(_git_ps1)'

    # Current directory and newline
    PS1+="${maincolor} %~/
==>${norm} "

}
autoload -Uz add-zsh-hook
add-zsh-hook precmd hccprompt

# Show Elapsed wall-clock time for any command that takes longer
# than 1 second. This is like $REPORTTIME except that the threshold is
# real time rather than user+system CPU time.
__real_seconds() {
    echo $(( $(date '+%s + %N') / 1000000000.0 ))
}

__record_start_time() {
    # Used in elapsed time calculation in the prompt
    _start=$(__real_seconds)
}
add-zsh-hook preexec __record_start_time

__show_elapsed() {
    # Show how long the previous command took if more than 1 second
    if (($+_start)); then
        local elapsed=$(( $(__real_seconds)-_start ))
        unset _start
        (($elapsed > 1.0)) && printf "${hcyan}Elapsed: %.3f${norm}\n" $elapsed >&2
    fi
}
add-zsh-hook precmd __show_elapsed


# Emit a terminal bell if last command exited with non-zero status. This makes
# the tmux window label change to "alert" color. Used to do this in the prompt
# but it would re-asert alert status every time the prompt was redrawn, causing
# lots of false alerts. Doing it in precmd means it will happen only once.
__bell_on_error() {
    [[ $? != 0 ]] && echo -n ${PR_BELL}
}
add-zsh-hook precmd __bell_on_error

alias sp='source ~/.zsh/setprompt'
